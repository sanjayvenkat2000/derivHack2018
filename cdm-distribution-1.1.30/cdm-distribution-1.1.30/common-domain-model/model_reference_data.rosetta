namespace "org.isda.cdm"

class Account <"The account specification.">
	[synonym FpML value Account]
{
	id string (0..1) anchor;
		[synonym FpML value id]
	accountNumber string (1..1) scheme "accountIdScheme" <"The account number.">;
		[synonym FpML value accountId]
	accountName string (0..1) scheme "accountNameScheme" <"The name by which the account is known.">;
		[synonym FpML value accountName]
	accountType AccountTypeEnum (0..1) scheme "accountTypeScheme" <"The type of account, e.g. client, house.">;
		[synonym FpML value accountType]
	accountBeneficiary string (0..1) reference <"A reference to the party beneficiary of the account.">;
		[synonym FpML value accountBeneficiary]
	servicingParty string (0..1) reference <"The reference to the legal entity that services the account, i.e. in the books of which the account is held.">;
		[synonym FpML value servicingParty]
}

class LegalEntity <"A class to represent the attributes that are specific to a legal entity.">
{
	id string (0..1) anchor;
		[synonym FpML value id]
	entityId string (0..1) scheme "entityIdScheme" <"A legal entity identifier (e.g. RED entity code).">;
		[synonym FpML value entityId]
	name string (1..1) scheme "entityNameScheme" <"The legal entity name.">;
		[synonym FpML value partyName]
		[synonym FpML value entityName]
}

class NaturalPerson <"A class to represent the attributes that are specific to a natural person.">
{
	id string (0..1) anchor;
		[synonym FpML value id pathExpression "person"]
	honorific string (0..1) <"An honorific title, such as Mr., Ms., Dr. etc.">;
		[synonym FpML value honorific pathExpression "person"]
	firstName string (1..1) <"The natural person's first name. It is optional in FpML.">;
		[synonym FpML value firstName pathExpression "person"]
	middleName string (0..*);
		[synonym FpML value middleName pathExpression "person"]
	initial string (0..*);
		[synonym FpML value initial pathExpression "person"]
	surname string (1..1) <"The natural person's surname.">;
		[synonym FpML value surname pathExpression "person"]
	suffix string (0..1) <"Name suffix, such as Jr., III, etc.">;
		[synonym FpML value suffix pathExpression "person"]
	dateOfBirth date (0..1) <"The natural person's date of birth.">;
		[synonym FpML value dateOfBirth pathExpression "person"]
}

choice rule NaturalPerson_choice <"Choice rule to represent an FpML choice construct.">
	for NaturalPerson optional choice between
	middleName and initial

class NaturalPersonRole <"The class to specify the role(s) that natural person(s) may have in relation to the contract.">
{
	personReference string (1..1) reference <"A reference to the natural person to whom the role refers to.">;
		[synonym FpML value personReference]
	role NaturalPersonRoleEnum (0..1) scheme "personRoleScheme" <"FpML specifies a person role that is distinct from the party role.">;
		[synonym FpML value role] 
}

class Party <"The party class.">
	[synonym FpML value Party]
{
	id string (0..1) anchor;
		[synonym FpML value id]
	partyId string (1..*) scheme "partyIdScheme" <"The identifier associated with a party, e.g. the 20 digits LEI code.">;
		[synonym FpML value partyId]
	legalEntity LegalEntity (0..1);
	naturalPerson NaturalPerson (0..*);
}

class PartyAndAccountReference <"This class corresponds to the FpML PartyAndAccountReferences.model.">
{
	partyReference string (1..1) reference;
		[synonym FpML value partyReference]
	accountReference string (0..1) reference;
		[synonym FpML value accountReference]
}

class PartyRole <"The class to specify the role(s) that party(ies) may have in relation to the contract.">
{
	partyReference string (1..1) reference <"A reference to the party to which the role refers to.">;
		[synonym FpML value determiningParty pathExpression "trade"]
		[synonym FpML value barrierDeterminationAgent pathExpression "trade"]
		[synonym FpML value hedgingParty pathExpression "trade"]
		[synonym FpML value brokerPartyReference pathExpression "trade"]
	role PartyRoleEnum (1..1) <"The party role.">;
		[synonym FpML value determiningParty pathExpression "trade" set role to PartyRoleEnum.DeterminingParty]
		[synonym FpML value barrierDeterminationAgent pathExpression "trade" set role to PartyRoleEnum.BarrierDeterminationAgent]
		[synonym FpML value hedgingParty pathExpression "trade" set role to PartyRoleEnum.HedgingParty]
		[synonym FpML value brokerPartyReference pathExpression "trade" set role to PartyRoleEnum.Broker]
}